pkgname=songrec-git
_pkgname=songrec
pkgver=r425.ea12948
pkgrel=1
provides=('songrec')
conflicts=('songrec')
pkgdesc='An open-source, unofficial Shazam client for Linux, written in Rust.'
url='https://songrec.fossplant.re/'
arch=('x86_64')
license=('GPL3')
makedepends=('cargo' 'rust' 'pkgconf' 'git')
depends=('gtk4' 'alsa-lib' 'libpulse' 'pulseaudio-alsa' 'openssl' 'ffmpeg')
source=("$_pkgname::git+https://github.com/marin-m/songrec.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  cargo build --release
}

package() {
  cd "$_pkgname"
  install -Dm755 "${CARGO_TARGET_DIR:-target}/release/songrec" "$pkgdir/usr/bin/songrec"
  install -Dm755 "packaging/rootfs/usr/share/applications/re.fossplant.songrec.desktop" "$pkgdir/usr/share/applications/re.fossplant.songrec.desktop"
  install -Dm755 "packaging/rootfs/usr/share/icons/hicolor/scalable/apps/re.fossplant.songrec.svg" \
                    "$pkgdir/usr/share/icons/hicolor/scalable/apps/re.fossplant.songrec.svg"
  install -Dm755 "packaging/rootfs/usr/share/metainfo/re.fossplant.songrec.metainfo.xml" \
                    "$pkgdir/usr/share/metainfo/re.fossplant.songrec.metainfo.xml"
  cp -ra "locale" "$pkgdir/usr/share/"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
